#+TITLE: Emacs Configuration
#+AUTHOR: Tom Schutter

To update packages:

1. M-x package-list-packages
2. U (mark Upgradable packages)
3. x (eXecute the installs and deletions).

* Standard Directories

Configure where Emacs stores and finds various files.

** Define Python-like ~os-path-join~ function

See [[http://stackoverflow.com/questions/3964715/what-is-the-correct-way-to-join-multiple-path-components-into-a-single-complete][stackoverflow.com]].

#+begin_src emacs-lisp
(defun os-path-join (root &rest dirs)
  "Joins a series of directories together, like Python's os.path.join,
  (os-path-join \"/tmp\" \"a\" \"b\" \"c\") => /tmp/a/b/c"

  (if (not dirs)
      root
    (apply 'os-path-join
           (expand-file-name (car dirs) root)
           (cdr dirs))))
#+end_src

** Determine the location of the ~.emacs.d~ directory

#+begin_src emacs-lisp
(defvar emacs-d-directory (file-name-directory load-file-name))
#+end_src

** Add ~.emacs.d/elisp/~ to ~load-path~

Prepend ~.emacs.d/elisp/~ to ~load-path~.  We used to append all of
its subdirectories as well, but they no longer exist.

#+begin_src emacs-lisp
(add-to-list
 'load-path
 (file-name-as-directory (os-path-join emacs-d-directory "elisp")))
#+end_src

** Define ~emacs-cache-dir~ for location of state and cache files

#+begin_src emacs-lisp
(defvar emacs-cache-dir
  (file-name-as-directory
   (os-path-join
    (or (getenv "XDG_CACHE_HOME")
        (expand-file-name "~/.cache/"))
    "emacs")))
(make-directory emacs-cache-dir t)  ; create it if it does not exist
(set-file-modes emacs-cache-dir #o700)  ; and make it private
#+end_src

** Put all backups in ~$HOME/.cache/emacs/backups/~

By default, Emacs saves backup files in the current directory.  See
[[http://www.emacswiki.org/emacs/BackupDirectory][emacswiki BackupDirectory]].

#+begin_src emacs-lisp
(defvar backup-directory (file-name-as-directory (os-path-join emacs-cache-dir "backups")))
(setq backup-directory-alist `((".*" . ,backup-directory)))
(setq vc-make-backup-files t)  ; even backup files under version control
#+end_src

** Create a backup every time a buffer is saved

By default, Emacs creates a backup only the first time you save a
buffer during a session.  This is incompatible with leaving Emacs and
its buffers open all the time and constantly making changes and
writing them to disk.

See [[https://www.emacswiki.org/emacs/ForceBackups][emacswiki Force Backups]].

#+begin_src emacs-lisp
(defun my/force-backup-of-buffer ()
    (setq buffer-backed-up nil))
(add-hook 'before-save-hook  'my/force-backup-of-buffer)
#+end_src

On 2017-10-08 I tried using the backup-each-save package from
[[https://www.emacswiki.org/emacs/BackupEachSave][emacswiki Backup Each Save]], but that did not work.

** Keep last 8 versions of backup files

Keep new versions but no ancient versions.

#+begin_src emacs-lisp
(setq version-control t
      delete-old-versions t
      kept-old-versions 0
      kept-new-versions 8)
#+end_src

** Put all auto-save files in ~$HOME/.cache/emacs/auto-save/~

See [[http://www.emacswiki.org/emacs/AutoSave][emacswiki AutoSave]].

#+begin_src emacs-lisp
(defvar auto-save-directory (file-name-as-directory (os-path-join emacs-cache-dir "auto-save")))
(setq auto-save-list-file-prefix auto-save-directory)
(setq auto-save-file-name-transforms `((".*" ,auto-save-directory t)))
#+end_src

** Cleanup extraneous session files created by x-win.el

When the window system is shutting down, emacs-session-save() is
called which creates session.X-SESSION-ID files in the
user-emacs-directory.

I have not discovered a way to prevent these files from being created,
so we just delete them if they exist.

#+begin_src emacs-lisp
(dolist (filename (file-expand-wildcards (os-path-join user-emacs-directory "session.*")))
  (delete-file filename))
#+end_src

* Server Mode

Start an Emacs server if it is not already running.

#+begin_src emacs-lisp
(require 'server)
;; Move server-auth-dir from .emacs.d to %TMP%.  If Emacs complains
;; that the directory is unsafe, change the ownership from
;; Administrators to yourself.
(if (eq system-type 'windows-nt)
    (setq server-auth-dir (file-name-as-directory (os-path-join (getenv "TMP") "emacs"))))
(unless (server-running-p)
  (server-start))
#+end_src

* Display

** Configure window title

#+begin_src emacs-lisp
(setq frame-title-format (concat "%b@" system-name))  ;%b = buffer name
#+end_src

** Set default font

Setting the font here is problematic because it triggers a window
resize, which may push the window off of the screen.

#+begin_src emacs-lisp
(if (and window-system (eq system-type 'windows-nt))
      (set-face-attribute 'default nil :font "Consolas-11"))
#+end_src

** Turn off blinking cursor

Blinking cursors are obnoxious.

#+begin_src emacs-lisp
(blink-cursor-mode 0)
#+end_src

** Make the cursor the width of the character under it

If a block cursor is over a tab, it will be drawn as wide as that tab
on the display.

#+begin_src emacs-lisp
(setq x-stretch-cursor t)
#+end_src

** Configure the mode line

Display the size of the buffer, line number, and column number in the
mode line.

#+begin_src emacs-lisp
(size-indication-mode 1)
(line-number-mode 1)
(column-number-mode 1)
#+end_src

Display the current function name in the mode line.

#+begin_src emacs-lisp
(which-function-mode 1)
#+end_src

** Display line numbers

Display line numbers on the left side of the window.  Always use 5
columns and display in all buffers.  See [[http://www.emacswiki.org/emacs/LineNumbers][emacswiki LineNumbers]].

#+begin_src emacs-lisp
(use-package nlinum
  :init
  (setq nlinum-format "% 5d")
  (global-nlinum-mode))
#+end_src

** Highlight uncommitted git changes

#+begin_src emacs-lisp
(when (display-graphic-p)
  (use-package git-gutter+
    :diminish git-gutter+-mode  ;; do not display in mode-line
    :config
    (progn
      (use-package git-gutter-fringe+)
      (global-git-gutter+-mode 1))))
#+end_src

** Enable advanced highlighting of matching parenthesis

Display highlighting on whatever parenthesis (and paired delimiter if
you like this) matches the one before or after point.

#+begin_src emacs-lisp
(use-package mic-paren
  :init
  (setq paren-sexp-mode t)  ; Always highlight the whole s-expression.
  :config
  (add-hook 'prog-mode-hook 'paren-activate)
  (add-hook 'LaTeX-mode-hook
            (function (lambda ()
                        (paren-toggle-matching-quoted-paren 1)
                        (paren-toggle-matching-paired-delimiter 1))))
  (add-hook 'c-mode-common-hook
            (function (lambda ()
                        (paren-toggle-open-paren-context 1)))))
#+end_src

* Files

Configure how files are selected and found.

** Selecting files in the minibuffer

Ignore case.

#+begin_src emacs-lisp
(setq read-file-name-completion-ignore-case t)
#+end_src

Ignore files with certain extensions.

#+begin_src emacs-lisp
(mapc (lambda (x)
        (add-to-list 'completion-ignored-extensions x))
      '(".exe" ".pdf"))
#+end_src

** Recent files

Enable File -> Open Recent.  This list is is automatically saved
across Emacs sessions.

See [[http://www.emacswiki.org/emacs/RecentFiles][emacswiki RecentFiles]].

#+begin_src emacs-lisp
(use-package recentf
  :demand
  :init
  (setq recentf-save-file (os-path-join emacs-cache-dir "recentf"))
  (recentf-mode 1)
  :bind
  ("<kp-4>" . recentf-open-files))
#+end_src

** Default to filename at point for ~C-x C-f~.

When opening a file using ~C-x C-f~, suggest the filename at point.  I
tried ido-mode, but I don't like the M-p, M-n behaviour.  See
[[http://www.emacswiki.org/emacs/FindFileAtPoint][emacswiki FindFileAtPoint]].

#+begin_src emacs-lisp
(require 'ffap)
(ffap-bindings)
(setq ffap-machine-p-known 'accept)   ; No pinging
(setq ffap-ftp-regexp nil)            ; Disable FTP
(setq ffap-ftp-sans-slash-regexp nil) ; Disable FTP

;;; On UNIX, all strings starting with / are recognized as a path.
;;; This is annoying especially on closing XML tags.
;;; The following advice ignores / as a wrong result.
(defadvice ffap-file-at-point (after ffap-file-at-point-after-advice ())
  "Advise ffap to ignore files starting with /."
  (if (string= ad-return-value "/")
      (setq ad-return-value nil)))
(ad-activate 'ffap-file-at-point)

;;; Check ffap string for line-number and goto it.
(defvar ffap-file-at-point-line-number nil
  "Variable to hold line number from the last `ffap-file-at-point' call.")
(defadvice ffap-file-at-point (after ffap-store-line-number activate)
  "Search `ffap-string-at-point' for a line number pattern and save it in `ffap-file-at-point-line-number' variable."
  (let* ((string (ffap-string-at-point)) ;; string/name definition copied from `ffap-string-at-point'
         (name
          (or (condition-case nil
                  (and (not (string-match "//" string)) ; foo.com://bar
                       (substitute-in-file-name string))
                (error nil))
              string))
         (line-number-string
          (and (string-match ":[0-9]+" name)
               (substring name (1+ (match-beginning 0)) (match-end 0))))
         (line-number
          (and line-number-string
               (string-to-number line-number-string))))
    (if (and line-number (> line-number 0))
        (setq ffap-file-at-point-line-number line-number)
      (setq ffap-file-at-point-line-number nil))))
(defadvice find-file-at-point (after ffap-goto-line-number activate)
  "If `ffap-file-at-point-line-number' is non-nil goto this line."
  (when ffap-file-at-point-line-number
    (goto-char (point-min))
    (forward-line (1- ffap-file-at-point-line-number))
    (setq ffap-file-at-point-line-number nil)))

;;; Search for files in directories other than the current.
;;;
;;; I was using ff-paths for this, but it breaks {svn,git} checkins,
;;; opening files that don't exist yet, TRAMP, and other things I have
;;; already forgotten.
;;;
;;; Add root directories to ffap-c-path in "~/.emacs-local.el":
;;;   (add-to-list 'ffap-c-path "~/src/myproj")
(add-to-list 'ffap-c-path "~/src")
(setq ffap-alist (append ffap-alist '(("\\.py\\'" . ffap-c-mode))))
#+end_src

** TRAMP remote file access

To activate, open file of the form /machine:localname
See http://www.gnu.org/software/tramp/

#+begin_src emacs-lisp
(require 'tramp)
(require 'tramp-cache)
(require 'tramp-sh)
(setq tramp-default-method "ssh")
(setq tramp-persistency-file-name (os-path-join emacs-cache-dir "tramp"))
(if (< emacs-major-version 24) ; broken in emacs-24
    (setq tramp-remote-process-environment
          (split-string
           (replace-regexp-in-string
            "HOME/\.tramp_history"
            "HOME/.cache/emacs/tramp_history"
            (mapconcat 'identity tramp-remote-process-environment "|"))
           "|")))  ; move ~/.tramp_history file created on target to ~/.cache/emacs
#+end_src

* Buffers

Buffer manipulation.

** Kill this buffer

Make C-x k kill this buffer instead of prompting for which buffer to
kill.  If I want to kill a different buffer, I use C-x C-b instead.

#+begin_src emacs-lisp
(global-set-key (kbd "C-x k") 'kill-this-buffer)
#+end_src

** Uniquely indentify buffers

Make two buffers with the same file name open distinguishable.

#+begin_src emacs-lisp
(use-package uniquify
  :ensure nil  ; https://github.com/jwiegley/use-package/issues/320
  :config
  (setq uniquify-after-kill-buffer-p t)     ; rename after killing uniquified
  (setq uniquify-ignore-buffers-re "^\\*")) ; don't muck with special buffers
#+end_src

** Enable switching between buffers using substrings

See [[http://www.emacswiki.org/emacs/InteractivelyDoThings][emacswiki InteractivelyDoThings (ido)]].

#+begin_src emacs-lisp
(use-package ido
  :init
  (ido-mode t))
#+end_src

** Switch between buffers

- ~C-S-tab~ - previous user buffer
- ~C-tab~ - next user buffer
- ~kp-subtract~ - close current buffer

#+begin_src emacs-lisp
(defun close-current-buffer ()
  "Close the current buffer.

   Similar to (kill-buffer (current-buffer)) with the following additions:

   - Prompt user to save if the buffer has been modified even if the
     buffer is not associated with a file.
   - Make sure the buffer shown after closing is a user buffer.

   A special buffer is one who's name starts with *.
   Else it is a user buffer."
  (interactive)
  (let (special-buffer-p is-special-buffer-after)
    (if (string-match "^*" (buffer-name))
        (setq special-buffer-p t)
      (setq special-buffer-p nil))

    ;; Offer to save buffers that are non-empty and modified, even for
    ;; non-file visiting buffer.  Because kill-buffer does not offer
    ;; to save buffers that are not associated with files.
    (when (and (buffer-modified-p)
               (not special-buffer-p)
               (not (string-equal major-mode "dired-mode"))
               (if (equal (buffer-file-name) nil)
                   (if (string-equal "" (save-restriction (widen) (buffer-string))) nil t)
                 t))
      (if (yes-or-no-p
           (concat "Buffer " (buffer-name) " modified; kill anyway? "))
          (save-buffer)
        (set-buffer-modified-p nil)))

    ;; close
    (kill-buffer (current-buffer))

    ;; if emacs buffer, switch to a user buffer
    (if (string-match "^*" (buffer-name))
        (setq is-special-buffer-after t)
      (setq is-special-buffer-after nil))
    (when is-special-buffer-after
      (next-user-buffer))))

(defun next-user-buffer ()
  "Switch to the next user buffer in cyclic order.
User buffers are those not starting with *."
  (interactive)
  (next-buffer)
  (let ((i 0))
    (while (and (string-match "^*" (buffer-name)) (< i 50))
      (setq i (1+ i)) (next-buffer))))

(defun previous-user-buffer ()
  "Switch to the previous user buffer in cyclic order.
User buffers are those not starting with *."
  (interactive)
  (previous-buffer)
  (let ((i 0))
    (while (and (string-match "^*" (buffer-name)) (< i 50))
      (setq i (1+ i)) (previous-buffer))))

(global-set-key (kbd "<C-S-iso-lefttab>") 'previous-user-buffer)
(global-set-key (kbd "<C-tab>") 'next-user-buffer)
(global-set-key (kbd "<kp-subtract>") 'close-current-buffer)
#+end_src

** Rename buffer and the file it is visiting

#+begin_src emacs-lisp
(defun rename-current-buffer-file ()
  "Renames current buffer and file it is visiting."
  (interactive)
  (let ((name (buffer-name))
        (filename (buffer-file-name)))
    (if (not (and filename (file-exists-p filename)))
        (error "Buffer '%s' is not visiting a file!" name)
      (let ((new-name (read-file-name "New name: " filename)))
        (if (get-buffer new-name)
            (error "A buffer named '%s' already exists!" new-name)
          (rename-file filename new-name 1)
          (rename-buffer new-name)
          (set-visited-file-name new-name)
          (set-buffer-modified-p nil)
          (message "File '%s' successfully renamed to '%s'"
                   name (file-name-nondirectory new-name)))))))
(global-set-key (kbd "C-x C-r") 'rename-current-buffer-file)
#+end_src

** Scratch buffer

*** Suppress initial message in scratch buffer; we know what it is for

#+begin_src emacs-lisp
(setq initial-scratch-message nil)
#+end_src

*** Respawn the scratch buffer if it is killed (C-x k)

#+begin_src emacs-lisp
(defun kill-scratch-buffer ()
  "Kill the *scratch* buffer and then respawn it."
  ;; The next line is just in case someone calls this manually
  (set-buffer (get-buffer-create "*scratch*"))

  ;; Kill the current (*scratch*) buffer
  (remove-hook 'kill-buffer-query-functions 'kill-scratch-buffer)
  (kill-buffer (current-buffer))

  ;; Make a brand new *scratch* buffer
  (set-buffer (get-buffer-create "*scratch*"))
  (lisp-interaction-mode)
  (make-local-variable 'kill-buffer-query-functions)
  (add-hook 'kill-buffer-query-functions 'kill-scratch-buffer)

  ;; Since we killed it, don't let caller do that.
  nil)
(kill-scratch-buffer)  ;install the hook
#+end_src

* Navigation

** Searching

When searching forward [Return] ends the search, but puts the point at
the end of the found text.  Define [Ctrl+Return] to put point at the
beginning.  See [[http://www.emacswiki.org/emacs/ZapToISearch][emacswiki ZapToISearch]].

#+begin_src emacs-lisp
(defun isearch-exit-other-end (rbeg rend)
  "Exit isearch, but at the other end of the search string (RBEG REND).
This is useful when followed by an immediate kill."
  (interactive "r")
  (isearch-exit)
  (goto-char isearch-other-end))
(define-key isearch-mode-map [(control return)] 'isearch-exit-other-end)
#+end_src

** Goto line

Provide an easy goto-line (~C-c g~).

#+begin_src emacs-lisp
(global-set-key (kbd "C-c g") 'goto-line)
#+end_src

** Better beginning, end of line

Switch between various line positions, like moving to the
beginning/end of code, line or comment.

#+begin_src emacs-lisp
(use-package mwim
  :init
  (global-set-key (kbd "C-a") 'mwim-beginning-of-code-or-line)
  (global-set-key (kbd "C-e") 'mwim-end-of-code-or-line)
  (global-set-key (kbd "<home>") 'mwim-beginning-of-line-or-code)
  (global-set-key (kbd "<end>") 'mwim-end-of-line-or-code))
#+end_src

** Track EOL

Vertical motion starting at EOL keeps to EOL.

#+begin_src emacs-lisp
(setq track-eol t)
#+end_src

** Scroll one line at a time instead of paging

Paging is what ~PgUp~ and ~PgDn~ are for.

#+begin_src emacs-lisp
(setq scroll-conservatively 100)
#+end_src

** PgUp and PgDn as inverse functions

Remember and restore point location after ~PgUp~ and ~PgDn~.

#+begin_src emacs-lisp
(setq scroll-preserve-screen-position t)
#+end_src

** Bookmarks

Bookmarking commands:

- ~C-x r m~ - set a bookmark at the current location
- ~C-x r b~ - jump to a bookmark
- ~C-x r l~ - list your bookmarks
- ~M-x bookmark-delete~ - delete a bookmark by name

See [[http://emacswiki.org/emacs/BookMarks][emacswiki BookMarks]].

#+begin_src emacs-lisp
(use-package bookmark
  :config
  (setq bookmark-default-file (os-path-join emacs-cache-dir "emacs.bmk"))

  :bind
  (("<kp-1>" . bookmark-bmenu-list)
   ("<kp-2>" . bookmark-set)
   ("<kp-3>" . bookmark-jump)))
#+end_src

** Save and restore point (F3, F4)

#+begin_src emacs-lisp
(define-key global-map (kbd "C-<f3>") '(lambda () (interactive) (point-to-register 33)))  ;^F3 Save
(define-key global-map (kbd "<f3>") '(lambda () (interactive) (jump-to-register 33)))     ; F3 Restore
(define-key global-map (kbd "C-<f4>") '(lambda () (interactive) (point-to-register 34)))  ;^F4 Save
(define-key global-map (kbd "<f4>") '(lambda () (interactive) (jump-to-register 34)))     ; F4 Restore
#+end_src

** Jump between symbols

Jump between symbols in your buffer, based on the initial symbol your point was on when you started the search.

- ~M-n~ - jump to next symbol
- ~M-p~ - jump to previous symbol

See https://github.com/mickeynp/smart-scan.

#+begin_src emacs-lisp
(use-package smartscan
  :init
  (global-smartscan-mode 1))
#+end_src

** Goto last change in current buffer

Move through points at which you made edits in a buffer.

- ~C-c C-,~ - goto last change
- ~C-c C-.~ - goto next change

#+begin_src emacs-lisp
(use-package goto-chg
  :bind
  (("C-c C-," . goto-last-change)
   ("C-c C-." . goto-last-change-reverse)))
#+end_src

** Move between windows with shift-arrow keys

#+begin_src emacs-lisp
(global-set-key (kbd "S-<left>") 'windmove-left)
(global-set-key (kbd "S-<right>") 'windmove-right)
(global-set-key (kbd "S-<up>") 'windmove-up)
(global-set-key (kbd "S-<down>") 'windmove-down)
#+end_src

** Expand Region

Increases the selected region by semantic units. Just keep pressing
the C-= until it selects what you want.

An example:

  : (setq alphabet-start "abc def")

With the cursor at the c, it starts by marking the entire word abc,
then expand to the contents of the quotes abc def, then to the entire
quote "abc def", then to the contents of the sexp setq alphabet-start
"abc def" and finally to the entire sexp.

#+begin_src emacs-lisp
(use-package expand-region
  :bind ("C-=" . er/expand-region))
#+end_src

** Dired

Show the file from point in the other window.  Use down/up or C-n/C-p
to display a different file.  Use SPC to scroll the peeped file down,
and C-SPC or backspace to scroll it up.

#+begin_src emacs-lisp
(use-package peep-dired
  :ensure t
  :defer t ; don't access `dired-mode-map' until `peep-dired' is loaded
  :config
  ;; kill peep buffers when peep mode is disabled
  (setq peep-dired-cleanup-on-disable t)
  ;; enable peeping when visiting directories from a peep-enabled directory
  (setq peep-dired-enable-on-directories t)
  ;; ignore certain files
  (setq peep-dired-ignored-extensions '("iso"))
  :bind (:map dired-mode-map
              ("P" . peep-dired)))
#+end_src

* Writing

** Set default major mode to text-mode

Set default major mode to be text-mode instead of fundamental-mode.
Although the doc says that default-major-mode is obsolete since 23.2
and to use major-mode instead, setting major-mode doesn't work.

#+begin_src emacs-lisp
(setq default-major-mode 'text-mode)
#+end_src

** Delete selected text when typing

All other editors work this way, so let's not confuse ourselves.

#+begin_src emacs-lisp
(require 'delsel)  ;required for OpenSUSE-12.1 emacs-23.3-6.1.2
(delete-selection-mode 1)
#+end_src

** Join lines

- ~M-^~ - join current line to one above
- ~M-j~ - join current line to one below

#+begin_src emacs-lisp
(global-set-key (kbd "M-j") (lambda () (interactive) (join-line -1)))
#+end_src

** Clean up spaces

Cycle between 1, 0, or original spaces around point where spaces
includes newlines and tabs.

#+begin_src emacs-lisp
(if (and (>= emacs-major-version 24)
         (>= emacs-minor-version 4))
    (global-set-key (kbd "M-SPC") 'cycle-spacing)
  ;; Just delete newlines as well as spaces and tabs around point.
  (global-set-key (kbd "M-SPC") '(lambda () (interactive) (just-one-space -1))))
#+end_src

** Unfill paragraph

Press "M-Q" to perform the inverse of fill-paragraph ("M-q").  From
[[http://pages.sachachua.com/.emacs.d/Sacha.html][Sacha Chua]].

#+begin_src emacs-lisp
(defun my/unfill-paragraph (&optional region)
    "Takes a multi-line paragraph and makes it into a single line of text."
    (interactive (progn
                   (barf-if-buffer-read-only)
                   (list t)))
    (let ((fill-column (point-max)))
      (fill-paragraph nil region)))
(bind-key "M-Q" 'my/unfill-paragraph)
#+end_src

** Insert datetime into current buffer (C-c i d, C-c i t)

By default, the inserted date or datetime is enclosed in square
brackets for org formatting.  Use C-u to insert without the brackets.

#+begin_src emacs-lisp
(defun my-insert-date (universal)
  "Insert date string into current buffer."
  (interactive "P")
  (insert (format-time-string (if universal "%Y-%m-%d" "[%Y-%m-%d]"))))
(global-set-key (kbd "C-c i d") 'my-insert-date)

(defun my-insert-date-time (universal)
  "Insert date time string into current buffer."
  (interactive "P")
  (insert (format-time-string (cond
                               ((equal universal '(4))  ; C-u
                                "%Y-%m-%d %H:%M")
                               ((equal universal '(16))  ; C-u C-u
                                "%Y-%m-%dT%H:%M")
                               (t
                                "[%Y-%m-%d %H:%M]")))))
(global-set-key (kbd "C-c i t") 'my-insert-date-time)
#+end_src

** Make URL human readable

Key binding?

#+begin_src emacs-lisp
(require 'url-humanify)  ; in ./elisp/
;theoretically the following should work, but it does not
;(use-package url-humanify
;  :load-path "./elisp/")
#+end_src

** Whitespace

*** Indentation should insert spaces, not tabs

#+begin_src emacs-lisp
(setq-default indent-tabs-mode nil)
#+end_src

*** Display and cleanup bogus whitespace

See [[http://www.emacswiki.org/emacs/WhiteSpace][emacswiki WhiteSpace]].

#+begin_src emacs-lisp
(use-package whitespace
  :config
  (setq whitespace-style
        '(face trailing tabs empty indentation space-before-tab))
  (global-whitespace-mode 1)
  (setq whitespace-action '(auto-cleanup))
  (add-hook 'makefile-mode-hook
            (lambda () (setq indent-tabs-mode t)))
  (defun whitespace-disable-for-some-files ()
    "Disable whitespace mode for some files."
    (let ((extension (file-name-extension buffer-file-name)))
      (if (or (string-equal extension "sln")
              (string-match "sigrok" buffer-file-name))
          (progn
            (set (make-local-variable 'whitespace-style) '(nil))
            (set (make-local-variable 'whitespace-action) '(nil))
            (set (make-local-variable 'indent-tabs-mode) t)))))
  (add-hook 'find-file-hook 'whitespace-disable-for-some-files))
#+end_src

*** If we do see tabs, they are 4 chars wide

#+begin_src emacs-lisp
(setq-default tab-width 4)
#+end_src

*** Require final newline

If there is no newline at the end of the file, append one when saving.
This should not be necessary because most modes should set
require-final-newline to mode-require-final-newline, but most do not
(Emacs-Lisp for one).  The risk here is if we open a binary file we
might append a newline.

#+begin_src emacs-lisp
(setq require-final-newline t)
#+end_src

*** Identify variables that are safe to be set as file variables ??

#+begin_src emacs-lisp
(put 'whitespace-line-column 'safe-local-variable 'integerp)
#+end_src

** Entering special characters with C-q

Use hex radix when entering special characters with C-q.  Default is
octal.  For example, C-q 2 0 a c <return> will enter "â‚¬".

#+begin_src emacs-lisp
(setq read-quoted-char-radix 16)
#+end_src

* Desktop

Saves the state of Emacs from one session to another. The buffers,
their file names, major modes, buffer positions, and so on are saved.

See [[http://www.emacswiki.org/emacs/DeskTop][emacswiki DeskTop]].

#+begin_src emacs-lisp
(use-package desktop
  :if window-system
  :demand
  :config
  (desktop-save-mode 1)
  (setq desktop-base-file-name "desktop")  ;no need for leading dot
  (setq desktop-base-lock-name "desktop.lock")  ;no need for leading dot
  (setq desktop-path (list emacs-cache-dir))
  (setq desktop-load-locked-desktop nil)  ;do not load desktop if locked
  (add-to-list 'desktop-globals-to-save 'query-replace-history)  ; C-%
  (add-to-list 'desktop-globals-to-save 'log-edit-comment-ring)  ; *VC-log*
  (add-to-list 'desktop-globals-to-save 'bookmark-history)       ; C-x r b

  ;; Clean stale desktop.lock file.
  (defun emacs-process-p (pid)
    "If PID is the process ID of an Emacs process, return t, else nil.
     Also returns nil if pid is nil."
    (when pid
      (let ((attributes (process-attributes pid)) (cmd))
        (dolist (attr attributes)
          (if (string= "comm" (car attr))
              (setq cmd (cdr attr))))
        (if (and cmd (or (string= "emacs" cmd) (string= "emacs.exe" cmd))) t))))

  (defadvice desktop-owner (after pry-from-cold-dead-hands activate)
    "Do not allow dead emacsen to own the desktop file."
    (when (not (emacs-process-p ad-return-value))
      (setq ad-return-value nil))))
#+end_src

* Org Mode

#+begin_src emacs-lisp
(require 'org)
#+end_src

** Global key bindings

I was using "C-c o" as a common prefix, but that did not always work.
For example, using "\Col" with org-store-link gives a "Bad URL" error.

#+begin_src emacs-lisp
  ; do not use lambda so that which-key can be informative
(defun change-to-org-notes-buffer ()
  "Change to default org notes buffer."
  (interactive) (find-file org-default-notes-file))
(global-set-key "\C-coh" 'change-to-org-notes-buffer)
(global-set-key "\C-cl" 'org-store-link)
(global-set-key "\C-c\C-l" 'org-insert-link)
#+end_src

Wrap region.  Select a region and then press ~"~, ~'~, ~(~, ~{~, or
~[~.  See [[https://github.com/rejeep/wrap-region.el][wrap-region package]].

#+begin_src emacs-lisp
(use-package wrap-region
  :config
  (add-hook 'org-mode-hook 'wrap-region-mode))
#+end_src

- * = *bold*
- ~ = ~code~
- + = +strike-through+
- l = begin_src emacs_lisp
- p = begin_src python
- s = begin_src shell (sh)
- e = begin_example

#+begin_src emacs-lisp
(wrap-region-add-wrapper "*" "*" nil 'org-mode)
(wrap-region-add-wrapper "~" "~" nil 'org-mode)
(wrap-region-add-wrapper "+" "+" nil 'org-mode)
(wrap-region-add-wrapper "#+begin_src emacs-lisp\n" "#+end_src\n" "l" 'org-mode)
(wrap-region-add-wrapper "#+begin_src python\n" "#+end_src\n" "p" 'org-mode)
(wrap-region-add-wrapper "#+begin_src shell\n" "#+end_src\n" "s" 'org-mode)
(wrap-region-add-wrapper "#+begin_example\n" "#+end_example\n" "e" 'org-mode)
#+end_src

Insert a TODO or TASK item before the current item, no matter where we
are in the current item.

#+begin_src emacs-lisp
(define-key org-mode-map (kbd "C-c i o")
  (lambda () (interactive)
    (org-forward-heading-same-level 0)
    (org-insert-heading)
    (insert "TODO ")))

(define-key org-mode-map (kbd "C-c i k")
  (lambda () (interactive)
    (org-forward-heading-same-level 0)
    (org-insert-heading)
    (insert "TASK ")))
#+end_src

** Capturing

Globally bind org-capture to "C-c c".

#+begin_src emacs-lisp
(global-set-key "\C-cc" 'org-capture)
#+end_src

Set default target for storing captured notes.

#+begin_src emacs-lisp
(setq org-default-notes-file (concat org-directory "/inbox.org"))
#+end_src

Define template strings.
See http://orgmode.org/manual/Template-expansion.html#Template-expansion

#+begin_src emacs-lisp
(defvar
  my/org-basic-task-template
  "* TODO %^{TODO}\n:LOGBOOK:\n- Created on [%<%Y-%m-%d %a %H:%M>]\n:END:\n%?%i")
#+end_src

Define templates used by org-capture ("C-c c").

#+begin_src emacs-lisp
(setq org-capture-templates
      `(("t" "TODO" entry
         (file "")  ; "" = use org-default-notes-file
         ,my/org-basic-task-template
         :kill-buffer)
        ("j" "Journal" entry
         (file+olp+datetree ,(concat org-directory "/journal.org"))
         "- %?"
         :kill-buffer)))
#+end_src

Take an URL from the clipboard and insert an org-mode link with a
title of a page found by the URL into the current buffer.

#+begin_src emacs-lisp
(use-package org-cliplink
  :bind
  (("C-c o l" . org-cliplink)))
#+end_src

** Navigation
Globally bind org-iswitchb to "C-c b".

#+begin_src emacs-lisp
(global-set-key "\C-cb" 'org-iswitchb)
#+end_src

Configure org-refile ("C-c C-w") to use top three heading levels from
all org agenda files.

#+begin_src emacs-lisp
(setq org-refile-targets '((org-agenda-files . (:maxlevel . 3))))
#+end_src

When M-RET is pressed, go to the end of the line before making a new
entry.  The default is to split the line at cursor position, which I
rarely want to do.

#+begin_src emacs-lisp
(setq org-M-RET-may-split-line nil)
#+end_src

Pressing RET on a link will follow the link.

#+begin_src emacs-lisp
(setq org-return-follows-link t)
#+end_src

** Structure Editing

Do not leave a blank line before a new heading/item or when moving
headings/items around.  The default behavior is to look at the
surrounding headings/items and try to make an intelligent decision
whether to insert a blank line or not.  But that ends up being
confusing; why did it leave a blank line this time but not last time?
#+begin_src emacs-lisp
(setf org-blank-before-new-entry '((heading . nil) (plain-list-item . nil)))
#+end_src

** Hard indentation

Electric indent mode is obnoxious when adding bullet lists.  When
pressing return at the end of a list, it always indents the new item
one more level.

#+begin_src emacs-lisp
(add-hook 'after-change-major-mode-hook (lambda() (electric-indent-mode -1)))
#+end_src

** Visualization

*** Indent text according to outline structure

See [[https://github.com/syl20bnr/spacemacs/issues/1833][Weird org + git gutter indentation bug #1833]].

#+begin_src emacs-lisp
(setq org-startup-indented t)
#+end_src

*** Disable "/" as indicating italics

As I type path names, I don't want the text to bounce between italics
and normal typefaces.  I rarely use italics anyways.

#+begin_src emacs-lisp
(delete '("/" italic "<i>" "</i>") org-emphasis-alist)
#+end_src

*** Ellipsis

Change the ellipsis "..." to something shorter to reduce visual
clutter.

- "\u2026" = HORIZONTAL ELLIPSIS
- "\u21b4" = RIGHTWARDS ARROW WITH CORNER DOWNWARDS
- "\u2935" = ARROW POINTING RIGHTWARDS THEN CURVING DOWNWARDS
  Note that this doesn't display correctly; empty boxes for all but
  the last.  2015-11-04.

#+begin_src emacs-lisp
(setq org-ellipsis "\u2026")
#+end_src

*** Fine tune characters are allowed before and after the markup characters

By default, commas are now allowed next to markup characters.  For
example, ~code,~ is not rendered as code.  This is a problem in this
very file when doing markup of key bindings like ~C-c C-,~.  See
[[http://stackoverflow.com/a/24173780][stackoverflow: How can I emphasize or verbatim quote a comma in org
mode?]].

#+begin_src emacs-lisp
(setcar (nthcdr 2 org-emphasis-regexp-components) " \t\r\n\"'")
(org-set-emph-re 'org-emphasis-regexp-components org-emphasis-regexp-components)
#+end_src

** Refiling

The default behavior when refiling items is to show a bag of
headlines.  But many of my headlines are the same, like "Tasks".  So
instead of just listing headlines when refiling, show them as a path
including the filename.  Like file.org/level1/level2.

#+begin_src emacs-lisp
(setq org-refile-use-outline-path 'file)
#+end_src

Automatically save your all .org files at regular intervals (10
seconds) because I often forget to do so.  Especially inbox.org.

#+begin_src emacs-lisp
(use-package real-auto-save
  :config
  (add-hook 'org-mode-hook 'real-auto-save-mode))
#+end_src

** Journal and Archiving

Archive a task with org-archive-subtree ("C-c C-x C-s"), by inserting
it into a date tree in journal.org.

#+begin_src emacs-lisp
(setq org-archive-location (concat org-directory "/journal.org::datetree/"))
#+end_src

** Google calendar sync

Bi-directional syncing of Google calendars.

See:
- [[http://cestlaz.github.io/posts/using-emacs-26-gcal/][Using Emacs - 26 - Google Calendar, Org Agenda]]
- [[https://github.com/myuhe/org-gcal.el][org-gcal package]]

This relies on the setting of org-gcal-client-id and
org-gcal-client-secret in ~/.emacs-local.el

#+begin_src emacs-lisp
(use-package org-gcal
  :ensure t
  :init
  (setq org-gcal-dir (concat emacs-cache-dir "org-gcal/"))
  :config
  (setq org-gcal-file-alist
    '(("tschutter65@gmail.com" .  "~/org/gcal-tschutter65.org"))))

;      ("oj9ms3gjljeseg3f37hejalr80@group.calendar.google.com" . "~/org/gcal-corelogic.org"))))
#+end_src

Sync gcal whenever the agenda is loaded.  Since this happens in the
background, if something has just been added to the calendar, the
agenda might need to be reloaded by pressing r in the agenda view.

#+begin_src emacs-lisp
(add-hook 'org-agenda-mode-hook (lambda () (org-gcal-sync)))
#+end_src

** Org Agenda

Globally bind org-agenda to "C-c a".

#+begin_src emacs-lisp
(global-set-key "\C-ca" 'org-agenda)
#+end_src

*** Specify where to look for agenda files

#+begin_src emacs-lisp
(setq org-agenda-files '())
(if (file-directory-p "~/org/")
    (add-to-list 'org-agenda-files "~/org/"))
#+end_src

*** Agenda custom commands

#+begin_src emacs-lisp
(setq org-agenda-custom-commands
       `(;; match those that are not scheduled.
         ;; from http://stackoverflow.com/questions/17003338
         ("u" "Unscheduled tasks" tags "-SCHEDULED={.+}/!+TODO|+STARTED|+WAITING")
         ("n" "Agenda and all TODO's" ((agenda "") (alltodo)))))
#+end_src

*** Display two weeks in agenda

#+begin_src emacs-lisp
(setq org-agenda-span 14)
#+end_src

*** Start agenda on today, not Monday

#+begin_src emacs-lisp
(setq org-agenda-start-on-weekday nil)
#+end_src

*** Highlight entire selected agenda line

#+begin_src emacs-lisp
(add-hook 'org-agenda-finalize-hook (lambda () (hl-line-mode)))
#+end_src

*** Warn of any deadlines in next 7 days

Default is 14 days.  I can't think about things due next week.

#+begin_src emacs-lisp
(setq org-deadline-warning-days 7)
#+end_src

*** Color items in the agenda
Hopefully this is temporary until a better way is found.

Use ~M-x list-colors-display~ for color names.

#+begin_src emacs-lisp
(defun org-agenda-color-category (category backcolor forecolor)
  (let ((re (rx-to-string `(seq bol (0+ space) ,category ":"))))
    (save-excursion
      (message (concat "coloring: '" category "'")
      (goto-char (point-min))
      (while (re-search-forward re nil t)
        (add-text-properties (match-beginning 0) (match-end 0)
                             (list 'face (list :background backcolor :foreground forecolor))))))))

; Try to match Google Calendar.
; (setq org-agenda-finalize-hook nil)  ; to reset hook
(add-hook 'org-agenda-finalize-hook
          (lambda ()
            (save-excursion
              (org-agenda-color-category "gcal-tschutter65" "#616161" "#FFFFFF")  ; gray
              (org-agenda-color-category "gcal-corelogic" "#795548" "#FFFFFF")  ; brown
              (org-agenda-color-category "gcal-linda"  "#F6BF26" "#FFFFFF"))))  ; yellow
#+end_src

** Org Babel begin_src code blocks

Enable languages for #+begin_src blocks.

#+begin_src emacs-lisp
(org-babel-do-load-languages
 'org-babel-load-languages
 '((ditaa . t)
   (emacs-lisp . t)
   (gnuplot . t)
   (python . t)
   (shell . t)))

(use-package gnuplot)
#+end_src

Sometimes I use C-c ' to edit code blocks, and sometimes I just edit
them directly.  Do not add extra indentation when editing code blocks
using C-c '.

#+begin_src emacs-lisp
(setq org-edit-src-content-indentation 0)
#+end_src

Fontify code in code blocks when viewing in the org file (as opposed
to only when using C-c ').

#+begin_src emacs-lisp
(setq org-src-fontify-natively t)
#+end_src

Do not ask for confirmation when evaluating code blocks with ~C-c
C-c~.  Disabling confirmation may result in accidental evaluation of
potentially harmful code.  But I never evaluate code blocks from
external sources.

#+begin_src emacs-lisp
(setq org-confirm-babel-evaluate nil)
#+end_src

Replace results verbatim instead of replacing results with a table.
To restore the default for a specific code block, use ~#+begin_src
<lang> :results verbatim~

#+begin_src emacs-lisp
(setq org-babel-default-header-args
      (cons '(:results . "replace verbatim")
            (assq-delete-all :results org-babel-default-header-args)))
#+end_src

Automatically redisplay inline images after code block execution.

#+begin_src emacs-lisp
(defun fix-inline-images ()
  (when org-inline-image-overlays
    (org-redisplay-inline-images)))

(add-hook 'org-babel-after-execute-hook 'fix-inline-images)
#+end_src

Specify system location for ditaa.jar.

#+begin_src emacs-lisp
(setq org-ditaa-jar-path "/usr/share/ditaa/ditaa.jar")
#+end_src

** Org Drill

Conduct an interactive "drill sessions" using [[http://orgmode.org/worg/org-contrib/org-drill.html][org-drill]].

Unfortunately, org-drill is not available via any of the standard
package repositories.  I installed it as following the instructions at
[[https://stackoverflow.com/questions/34983106/how-to-install-org-drill][How to install org-drill?]]

cd ~/.emacs.d/elisp
wget https://bitbucket.org/eeeickythump/org-drill/raw/01b05cd7561ad69e5ec9c1200414d4fa128c9a17/org-drill.el
wget http://orgmode.org/cgit.cgi/org-mode.git/plain/contrib/lisp/org-learn.el

#+begin_src emacs-lisp
(require 'org-drill)
(setq savehist-file (os-path-join emacs-cache-dir "savehist-history"))
(setq org-id-locations-file (os-path-join emacs-cache-dir "org-id-locations"))
#+end_src

* Calendar and Diary

#+begin_src emacs-lisp
(use-package calendar
  :config
  (add-hook 'today-visible-calendar-hook 'calendar-mark-today)
  (calendar-set-date-style 'iso)  ; parse dates in ~/diary
  (setq calendar-date-display-form
        '((format "%s-%.2d-%.2d, %s"
                  year
                  (string-to-number month)
                  (string-to-number day)
                  dayname)))  ; format displayed dates in diary
  (setq diary-number-of-entries 7)  ; number of days to display
  (setq diary-list-include-blanks t)  ; include empty days
  (add-hook 'list-diary-entries-hook 'sort-diary-entries t))  ; sort entries by time
#+end_src

** Encrypted diary handling

#+begin_src emacs-lisp
(load-library "mydiary")
#+end_src

* Shells
** shell-pop

Quickly pop up a shell.  See https://github.com/kyagi/shell-pop-el

#+begin_src emacs-lisp
(use-package shell-pop
  :bind
  (("C-$" . shell-pop)) ; $ is the shell prompt

  :config
  (setq shell-pop-window-position "left")  ; which really means right?
  (setq shell-pop-window-size 50)  ; currently ignored?
  (setq shell-pop-shell-type (quote ("ansi-term" "*ansi-term*" (lambda nil (ansi-term shell-pop-term-shell)))))
)
#+end_src

** Eshell

See [[http://www.emacswiki.org/emacs/CategoryEshell][emacswiki CategoryEshell]].

#+begin_src emacs-lisp
(use-package eshell
  :config
  (setq eshell-directory-name (file-name-as-directory (os-path-join emacs-cache-dir "eshell"))))
#+end_src

* Flycheck

See https://sourcegraph.com/github.com/robert-zaremba/flycheck

#+begin_src emacs-lisp
(use-package flycheck

  :bind
  (("<M-up>"   . flycheck-previous-error)
   ("<M-down>" . flycheck-next-error))

  :config
  ;; Enable flycheck mode in all buffers.
  (global-flycheck-mode)

  ;; On-the-fly spell checking.  See http://www.emacswiki.org/emacs/FlySpell
  (if (not (eq system-type 'windows-nt))
      (add-hook 'text-mode-hook 'turn-on-flyspell))

  ;; Save dictionary without confirmation.
  (setq ispell-silently-savep t)

  ;; Proselint English usage.
  (flycheck-define-checker proselint
      "A linter for prose."
      :command ("proselint" source-inplace)
      :error-patterns
      ((warning line-start (file-name) ":" line ":" column ": "
      (id (one-or-more (not (any " "))))
      (message) line-end))
      :modes (text-mode markdown-mode org-mode))
  (add-to-list 'flycheck-checkers 'proselint))
#+end_src

Most checkers have dependencies against external tools that perform
the checking. Use C-c ! ? to see what a checker needs, e.g. C-c ! ?
python-pylint.

JSON checking requires jsonlint.

#+begin_src shell
  sudo apt-get install nodejs-legacy npm
  sudo npm install jsonlint --global
#+end_src

Proselint requires proselint.

#+begin_src shell
  sudo pip install proselint
#+end_src

* Miscellaneous

** Startup
We don't need to see the startup message.

#+begin_src emacs-lisp
(setq inhibit-startup-message t)
#+end_src

And [[http://yann.hodique.info/blog/rant-obfuscation-in-emacs/][suppress the startup message in the echo area]] as well.

#+begin_src emacs-lisp
(put 'inhibit-startup-echo-area-message 'saved-value
     (setq inhibit-startup-echo-area-message (user-login-name)))
#+end_src

** Define word at point
Get a definition of the word at point or from the minibuffer.

#+begin_src emacs-lisp
(use-package define-word
  :ensure t
  :config
  (setq define-word-limit 20)
  :bind (("s-d" . define-word-at-point)
         ("s-D" . define-word)))
#+end_src

** Use PCRE instead of Emacs regex flavor

Although the Emacs flavor of regular expressions has interesting
features, it is yet one more syntax to learn.  Use ``pcre-mode`` to
make all commands that read regexps using the minibuffer use emulated
[[https://www.pcre.org/][Perl Compatible Regular Expression]] syntax instead of Emacs syntax.

#+begin_src emacs-lisp
(use-package pcre2el
  :config
  (pcre-mode))
#+end_src

** Printing

See [[http://www.emacswiki.org/emacs/PrintingFromEmacs][emacswiki PrintingFromEmacs]].

#+begin_src emacs-lisp
(use-package ps-print
  :config
  (setq ps-lpr-command "lp")
  (setq ps-number-of-columns 2)
  (setq ps-landscape-mode t)
  (setq ps-line-number t)
  (setq ps-print-color-p nil)
  (setq ps-print-header nil)
  (setq lpr-command "lp")
  (setq lpr-printer-switch "-d ")
  (setq lpr-add-switches nil)
  (setq lpr-page-header-switches '("-h" "%s" "-F" "--length=61" "--indent=4")))
#+end_src

** PDF editing and markup

# pdf-tools is in sad shape as of 2019-06-24.  pdf-tools-install fails.
[[http://pragmaticemacs.com/emacs/view-and-annotate-pdfs-in-emacs-with-pdf-tools/][# View and annotate PDFs in Emacs with pdf-tools]] and [[http://pragmaticemacs.com/emacs/more-pdf-tools-tweaks/][More pdf-tools tweaks]].
#
# The PDF-Tools package must be installed manually with M-x
# package-install, and then run M-x pdf-tools-install.
#
# Use C-c C-a to manipulate annotations.
#
# #+begin_src emacs-lisp
# (use-package pdf-tools
#   :pin manual ;; manually update
#
#   :config
#
#   ;; install PDF-Tools in all future PDF buffers
#   (pdf-tools-install)
#
#   ;; open pdfs scaled to fit page
#   (setq-default pdf-view-display-size 'fit-page)
#
#   ;; automatically annotate highlights
#   (setq pdf-annot-activate-created-annotations t)
#
#   ;; use normal isearch
#   (define-key pdf-view-mode-map (kbd "C-s") 'isearch-forward)
#
#   ;; turn off cua so copy works
#   (add-hook 'pdf-view-mode-hook (lambda () (cua-mode 0)))
#
#   ;; more fine-grained zooming
#   (setq pdf-view-resize-factor 1.1)
#
#   ;; linum mode is known to conflict with PDF-Tools
#   (add-hook 'pdf-view-mode-hook (lambda() (nlinum-mode -1)))
#
#   ;; keyboard shortcuts
#   (define-key pdf-view-mode-map (kbd "h") 'pdf-annot-add-highlight-markup-annotation)
#   (define-key pdf-view-mode-map (kbd "t") 'pdf-annot-add-text-annotation)
#   (define-key pdf-view-mode-map (kbd "D") 'pdf-annot-delete))
# #+end_src

# org-pdfview not installable since 2018-02-25:
#   https://github.com/markus1189/org-pdfview/issues/18
#
# Open links to PDF files in org mode.
#
# #+begin_src emacs-lisp
# (use-package org-pdfview
#   :config
#   ;;  (eval-after-load 'org '(require 'org-pdfview))
#   (add-to-list
#    'org-file-apps
#    '("\\.pdf\\'" . (lambda (file link) (org-pdfview-open link)))))
# #+end_src

** Web browsing

I use Chromium instead of Firefox because Firefox dropped the Keysnail
extension.

The ~browse-url-default-browser~ function is the default value for
~browse-url-browser-function~.  But rather than using the preferred
browser, it uses the first browser it finds and it looks for Firefox
before Chromium.

#+begin_src emacs-lisp
(setq browse-url-browser-function 'browse-url-chromium)
#+end_src

** Email

*** Outgoing mail

#+begin_src emacs-lisp
(require 'smtpmail)
(let* ((computername (downcase system-name))
       (prefixlen (min (length computername) 7))
       (prefix (substring computername 0 prefixlen))
       (realm
        (cond
         ((string-equal prefix "fdsv") "ISC")
         ((string-equal prefix "sps") "ISC")
         ((string-equal computername "apple") "ISC")
         ((string-equal computername "passion") "ISC")
         ((string-equal computername "wampi") "ISC")
         ((string-equal computername "wampi-win2003") "ISC")
         (t "HOME"))))
  (cond
   ((string-equal realm "ISC")
    (setq user-mail-address "tschutter@corelogic.com")
    (setq smtpmail-local-domain "corelogic.com")
    (setq smtpmail-smtp-server "smtp.corelogic.com"))
   (t
    (setq user-mail-address "t.schutter@comcast.net")
    (setq smtpmail-local-domain "schutter.home")
    (setq smtpmail-smtp-server "smtp.schutter.home"))))
;(setq smtpmail-debug-info t)  ;uncomment to debug problems
#+end_src

*** Composing mail

Use Message to compose mail.

#+begin_src emacs-lisp
(setq mail-user-agent 'message-user-agent)
(setq message-send-mail-function 'smtpmail-send-it)
(add-hook 'message-mode-hook 'turn-on-auto-fill) ;word wrap
#+end_src

*** Address book

Integration with Google contacts.

#+begin_src emacs-lisp
(require 'external-abook)  ; in ./elisp/
(custom-set-variables '(external-abook-command
                        (concat
                         (os-path-join emacs-d-directory "bin" "goobook-external-abook")
                         " query '%s'")))
#+end_src

Following is not working.

#+begin_src emacs-lisp
(eval-after-load "message"
  '(progn
     (add-to-list 'message-mode-hook
                  '(lambda ()
                     (local-unset-key "\C-c TAB")
                     (define-key
                       message-mode-map
                       "\C-c TAB"
                       'external-abook-try-expand)))))
#+end_src

** IM and Chat

*** ERC InternetRelayChat

#+begin_src emacs-lisp
(use-package erc
  :config
  (setq erc-nick "tschutter")
  (setq erc-prompt-for-password nil)
  (setq erc-autojoin-channels-alist
        '(("freenode.net" "#sigrok")))
  (setq erc-hide-list '("JOIN" "PART" "QUIT"))
  (setq erc-foolish-content '("^\*\*\* .*: topic set by "
                              "^\*\*\* .* modes: "
                              "^\*\*\* .* was created on"))

  (defun erc-foolish-content (msg)
    "Determine if MSG is foolish."
    (erc-list-match erc-foolish-content msg))
  (add-hook 'erc-insert-pre-hook
            (lambda (s)
              (when (erc-foolish-content s)
                (setq erc-insert-this nil))))

  ; no erc-log package in melpa
  ;(use-package erc-log
  ;  :init
  ;  (erc-log-enable)
  ;  :config
  ;  (setq erc-log-channels-directory (file-name-as-directory (os-path-join emacs-cache-dir "irclog"))))

  ; no easymenu package in melpa
  ;(use-package easymenu
  ;  :init
  ;  (easy-menu-add-item  nil '("tools") ["IRC with ERC" erc t]))
  )
#+end_src

*** BitlBee gateway to IM networks

sudo apt-get install bitlbee-libpurple
http://emacs-fu.blogspot.com/search/label/erc
http://wiki.bitlbee.org/quickstart
http://wiki.bitlbee.org/bitlbee-sipe

#+begin_src emacs-lisp
(defun bitlbee-identify ()
  "Generate a message identifying ourself."
  (when (and (string= "localhost" erc-session-server)
             (string= "&bitlbee" (buffer-name)))
    (erc-message "PRIVMSG" (format "%s identify user %s"
                                   (erc-default-target)
                                   bitlbee-password))))
(add-hook 'erc-join-hook 'bitlbee-identify)
(defun chat ()
  "Connect to IM networks using bitlbee."
  (interactive)
  (require 'secrets "secrets.el.gpg")  ; in ./elisp/
  (erc :server "localhost" :port 6667 :nick bitlbee-nick))
; register user BITLBEE-PASSWORD
; account add yahoo tom.schutter YAHOO-PASSWORD
#+end_src

* Programming

** VC (Version Control)

*** Display warning instead of asking when visiting VC file via simlink

#+begin_src emacs-lisp
(setq vc-follow-symlinks nil)
#+end_src

*** Put list of files in default commit message

#+begin_src emacs-lisp
(use-package log-edit
  :config
  (add-hook 'log-edit-hook
            (lambda ()
              (erase-buffer)  ; SETUP inserts stuff we don't want.
              (insert
               (mapconcat 'file-name-nondirectory (log-edit-files) ",")
               ": "))))
#+end_src

*** Navigate previous versions of a git controlled file

Visit a git-controlled file and issue ~M-x git-timemachine~.

Use the following keys to navigate historic version of the file:
  - ~p~ Visit previous historic version
  - ~n~ Visit next historic version
  - ~w~ Copy the abbreviated hash of the current historic version
  - ~W~ Copy the full hash of the current historic version
  - ~g~ Goto nth revision
  - ~q~ Exit the time machine.

#+begin_src emacs-lisp
(use-package git-timemachine)
#+end_src

*** Magit

[[https://magit.vc/][Magit]] is an interface to the version control system Git.

- [[https://magit.vc/manual/magit.html][Magit manual]]

#+begin_src emacs-lisp
(use-package magit
  :bind (("C-x g" . magit-status)))
#+end_src

** Compiling (F5)

The compiling section must come first, because it defines
smart-compile-alist which is updated by per-language sections.

*** Set compile command according to mode

#+begin_src emacs-lisp
(use-package smart-compile
  :demand  ; force loading immediately
  :config
  (add-to-list 'smart-compile-alist '(cmake-mode . "make -k"))
  (add-to-list 'smart-compile-alist '(python-mode . "pycheck %f -s")))
#+end_src

*** Force a vertical window split

#+begin_src emacs-lisp
(defadvice smart-compile (around split-horizontally activate)
  "Split window vertically when smart-compile is called."
  (let ((split-width-threshold nil)
        (split-height-threshold 0))
    ad-do-it))
(setq compilation-window-height 10)
#+end_src

*** Bind smart-compile to F5

#+begin_src emacs-lisp
(global-set-key [f5] 'smart-compile)
#+end_src

*** Globally enable C-n, C-p to cycle through errors

#+begin_src emacs-lisp
(defun my-next-error ()
  "Move point to next error and highlight it."
  (interactive)
  (progn
    (next-error)
    (deactivate-mark)
    (end-of-line)
    (activate-mark)
    (beginning-of-line)))
(defun my-previous-error ()
  "Move point to previous error and highlight it."
  (interactive)
  (progn
    (previous-error)
    (deactivate-mark)
    (end-of-line)
    (activate-mark)
    (beginning-of-line)))
(global-set-key (kbd "C-n") 'my-next-error)
(global-set-key (kbd "C-p") 'my-previous-error)
#+end_src

**** Other
Work in progress:
(setq compilation-scroll-output 'first-error)
(make-variable-buffer-local 'compile-command)
.dir.locals.el
    ((nil . ((eval . (set (make-local-variable 'my-project-path)
                          (file-name-directory
                           (let ((d (dir-locals-find-file ".")))
                             (if (stringp d) d (car d))))))
             (eval . (setq cmake-ide-build-dir (concat my-project-path "build")))
             (eval . (message "Project directory set to `%s'." my-project-path)))))


    ((nil .
          ((compile-command . "cd build && make"))))
projectile-project-compilation-cmd

** Projectile

Projectile is a project interaction library for Emacs that provides
easy project management and navigation.

Projects are marked by a .projectile file in the project root
directory.

#+begin_src emacs-lisp
(use-package projectile
  :init
  (setq projectile-keymap-prefix (kbd "C-x p"))  ; default is C-c p
  (setq projectile-enable-caching t)
  (setq projectile-completion-system 'default)
  (setq projectile-cache-file (os-path-join emacs-cache-dir "projectile.cache"))
  (setq projectile-known-projects-file (os-path-join emacs-cache-dir "projectile-bookmarks.eld")))
#+end_src

** GNU GLOBAL source code tagging system
Alternatives include
- Cscope.
- rtags handles C++ much better, but requires the C++ project be built
  with CMake.

#+begin_src emacs-lisp
(use-package ggtags
  :bind
  (("M-*" . pop-tag-mark)
   ("C-c t s" . ggtags-find-other-symbol)
   ("C-c t h" . ggtags-view-tag-history)
   ("C-c t r" . ggtags-find-reference)
   ("C-c t f" . ggtags-find-file)
   ("C-c t c" . ggtags-create-tags)
   ("C-c t u" . ggtags-update-tags))
  :init
  (add-hook 'c-mode-common-hook
            (lambda ()
              (when (derived-mode-p 'c-mode 'c++-mode 'java-mode 'asm-mode)
                (ggtags-mode 1)))))
#+end_src

** CEDET

[[http://cedet.sourceforge.net/][CEDET]] is a Collection of Emacs Development Environment Tools written
with the end goal of creating an advanced development environment in
Emacs.

;#+begin_src emacs-lisp
;  (use-package cedet
;    :init
;    (progn
;      ;; Add further minor-modes to be enabled by semantic-mode.  See
;      ;; doc-string of `semantic-default-submodes' for other things you can
;      ;; use here.
;      (dolist (submodes '(global-semantic-idle-summary-mode))
;        (add-to-list 'semantic-default-submodes submodes t))
;
;      ;; Enable Semantic
;      (semantic-mode 1)))
;#+end_src

** Common debugging

*** Display a variable's value in a tooltip
#+begin_src emacs-lisp
(gud-tooltip-mode)
#+end_src

*** Use the echo area instead of frames for GUD tooltips

Needs work.

#+begin_src emacs-lisp
;(setq gud-tooltip-echo-area t)
#+end_src

** Common Source Code Manipulation

*** Move current line up or down

<C-S-down> to move current line down.
<C-S-up> to move current line up.

#+begin_src emacs-lisp
(defun move-line-down ()
  "Move current line down."
  (interactive)
  (let ((col (current-column)))
    (save-excursion
      (forward-line)
      (transpose-lines 1))
    (forward-line)
    (move-to-column col)))
(defun move-line-up ()
  "Move current line up."
  (interactive)
  (let ((col (current-column)))
    (save-excursion
      (forward-line)
      (transpose-lines -1))
    (move-to-column col)))
(global-set-key (kbd "<C-S-down>") 'move-line-down)
(global-set-key (kbd "<C-S-up>") 'move-line-up)
#+end_src

*** Line wrap function call or function definition

Bound to <f2>.

#+begin_src emacs-lisp
(defun region-line-wrap ()
  "Line wrap region, breaking at commas."
  (let ((newline (if (eq major-mode (quote vbnet-mode)) " _\n" "\n")))
    (save-excursion
      (save-restriction
        (narrow-to-region (mark) (point))
        (goto-char (point-min))
        (forward-char)
        (if (not (looking-at newline))
            (insert newline))
        (while (re-search-forward "," (point-max) t)
          (if (not (looking-at newline))
              (insert newline)))
        (goto-char (point-max))
        (backward-char)
        (beginning-of-line)
        (if (not (looking-at " *)$"))
            (progn
              (goto-char (point-max))
              (backward-char)
              (insert newline)))))
    (indent-region (mark) (point) nil)))

(defun function-line-wrap ()
  "Line wrap function call or function definition."
  (interactive)
  (let ((original-point (point)))
    (save-excursion
      (mark-defun)
      (let ((defun-begin (point)) (defun-end (mark)))
        ;; Try the sexp that we are inside of.
        (goto-char original-point)
        ;; Move backward out of one level of parentheses (or blocks)
        ;; according to the mode.
        (funcall (key-binding (kbd "C-M-u")))
        (if (looking-at "(")
            (progn
              (set-mark (point))
              (forward-list)
              (region-line-wrap))
          ;; Try the sexp before original-point.
          (goto-char original-point)
          (re-search-backward ")" defun-begin)
          (backward-up-list)
          (set-mark (point))
          (forward-list)
          (region-line-wrap))))))

(define-key global-map (kbd "<f2>") '(lambda () (interactive) (function-line-wrap)))
#+end_src

** Python

*** Formatting

When filling docstrings, put the initial triple quotes are on their
own line, and do not put a blank line before the closing triple
quotes.  If the docstring can fit on one line, do so.

#+begin_src emacs-lisp
(setq python-fill-docstring-style 'symmetric)
#+end_src

*** Static code checks (either ^C-^W or ^C-^V)

#+begin_src emacs-lisp
(setq py-pychecker-command "pycheck")
(setq python-check-command "pycheck")
#+end_src

*** Simplify insertion of debugging print statements

#+begin_src emacs-lisp
(load "pyp.el")
#+end_src

*** Python editing

#+begin_src emacs-lisp
(add-hook 'python-mode-hook
          (lambda ()
            (if (not (eq system-type 'windows-nt))
                (flyspell-prog-mode))  ;on-the-fly spell check in comments
            (make-local-variable 'whitespace-style)
            (add-to-list 'whitespace-style 'lines-tail)  ;highlight cols beyond whitespace-line-column
            (define-key python-mode-map (kbd "C-c h") 'pylookup-lookup)  ;lookup in Python doc
            (define-key python-mode-map (kbd "<f12>") 'pyp)  ;insert debug print
            (define-key python-mode-map "\C-m" 'newline-and-indent)))
#+end_src

*** Python doc lookup

See https://github.com/tsgates/pylookup

Run "M-x pylookup-update-all" to update database.

#+begin_src emacs-lisp
(require 'pylookup)  ; in ./elisp/
(setq pylookup-program (os-path-join emacs-d-directory "bin" "pylookup.py"))  ;executable
(setq pylookup-db-file (os-path-join emacs-cache-dir "pylookup.db"))  ;database
(setq pylookup-html-locations '("/usr/share/doc/python2.7/html"))  ;doc source
(autoload 'pylookup-lookup "pylookup"
  "Lookup SEARCH-TERM in the Python HTML indexes." t)
#+end_src

*** Python ropemacs refactoring

Currently this is too expensive to do for all Python files, so we load
ropemacs only if requested.

#+begin_src emacs-lisp
(defun load-ropemacs ()
  "Load pymacs and ropemacs."
  (interactive)
  (require 'pymacs)
  (setq ropemacs-enable-shortcuts nil)
  (pymacs-load "ropemacs" "rope-")
  (define-key ropemacs-local-keymap (kbd "M-/") 'rope-code-assist)
  (define-key ropemacs-local-keymap (kbd "C-c C-d") 'rope-show-doc)
  (define-key ropemacs-local-keymap (kbd "C-c C-g") 'rope-goto-definition)
  (define-key ropemacs-local-keymap (kbd "C-c C-f") 'rope-find-occurrences)
  ;; Automatically save project python buffers before refactorings.
  (setq ropemacs-confirm-saving nil))
(global-set-key "\C-xpl" 'load-ropemacs)
#+end_src

*** Python vs. abbrev-mode

We don't use abbrev-mode explicitly, but elisp/python.el adds stuff to
python-mode-abbrev-table.  And then we are bothered about saving the
modified abbrevs.  So put the abbrev_defs file in var until we figure
it out.

#+begin_src emacs-lisp
(setq abbrev-file-name (os-path-join emacs-cache-dir "abbrev_defs"))
#+end_src

** Arduino

Major mode for arduino sketch (.ino) files.

#+begin_src emacs-lisp
(use-package arduino-mode
  :mode
  ("\\.ino\\'" . arduino-mode))
#+end_src

Reduce noise when compiling, and upload to Arduino by default.

#+begin_src emacs-lisp
(add-to-list 'smart-compile-alist '(arduino-mode . "make -k -s upload"))
#+end_src

** CMake

#+begin_src emacs-lisp
(use-package cmake-mode
  :mode (("\\.cmake\\'" . cmake-mode)
         ("CMakeLists\\.txt\\'" . cmake-mode))
  :config
  (add-hook 'cmake-mode-hook
            (lambda ()
              (setq-default cmake-tab-width 4))))
#+end_src

** C

#+begin_src emacs-lisp
(defun adjust-indentation-style ()
  "Adjust C indentation style."
  ;; use C-c C-s to determine the syntactic symbol
  ;; use C-h v c-offsets-alist to see current setting for the
  ;; syntactic symbol
  (c-set-offset 'arglist-intro '+)  ; normal indent for first arg
  (c-set-offset 'case-label '+)  ; indent case, not flush w/ switch
  (c-set-offset 'arglist-close '0))  ; no indent for close paren
(add-hook 'c-mode-hook 'adjust-indentation-style)
#+end_src

** C++

#+begin_src emacs-lisp
(require 'c-includes)
(add-to-list 'auto-mode-alist '("\\.h\\'" . c++-mode))
(add-hook 'c++-mode-hook 'adjust-indentation-style)
(add-hook 'c++-mode-hook
          (lambda ()
            (define-key-after c++-mode-map
              [menu-bar C++ List\ Included\ Files\ Sep]
              '(menu-item "----"))
            (define-key-after c++-mode-map
              [menu-bar C++ List\ Included\ Files]
              '(menu-item "List Included Files" c-includes-current-file))
            (if (not (eq system-type 'windows-nt))
                (flyspell-prog-mode))
            (setq-default c-basic-offset 4)))
#+end_src

** C#

See [[http://www.emacswiki.org/emacs/CSharpMode][emacswiki CSharpMode]].

#+begin_src emacs-lisp
(use-package csharp-mode
  :mode ("\\.cs\\'" . csharp-mode)
  :config
  (add-hook 'csharp-mode-hook 'adjust-indentation-style)
  (add-hook 'csharp-mode-hook
            (lambda ()
              (if (not (eq system-type 'windows-nt))
                  (flyspell-prog-mode)))))
#+end_src

** Java

#+begin_src emacs-lisp
(add-hook 'java-mode-hook 'adjust-indentation-style)
#+end_src

** reStructuredText

See [[http://www.emacswiki.org/emacs/reStructuredText][emacswiki reStructuredText]].

#+begin_src emacs-lisp
(defun rst-compile-html-preview ()
  "Compile a rst file to html and view in a browser."
  (interactive)
  (let*
      ((bufname (file-name-nondirectory buffer-file-name))
       (basename (file-name-sans-extension bufname))
       (outname (os-path-join temporary-file-directory (concat basename ".html"))))
    (set (make-local-variable 'compile-command)
         (concat "rst2html --verbose " bufname " " outname))
    (call-interactively 'compile)
    (browse-url-of-file outname)))
(add-to-list 'smart-compile-alist '(rst-mode rst-compile-html-preview))
#+end_src

** OpenSCAD

[[http://www.openscad.org/][OpenSCAD]] is software for creating solid 3D CAD objects.

#+begin_src emacs-lisp
(use-package scad-mode
  :mode ("\\.scad\\'" . scad-mode))
#+end_src

* Keyboard and Mouse

This section is last to override any keymappings of various packages.

** Define various key bindings

See [[http://ergoemacs.org/emacs/keystroke_rep.html][Emacs's Key Syntax Explained]].

Zoom in and out by changing font size like Firefox.

#+begin_src emacs-lisp
(global-set-key (kbd "C-+") 'text-scale-increase)
(global-set-key (kbd "C--") 'text-scale-decrease)   ; overrides negative-argument (still available via M--)
(global-set-key (kbd "C-0") (lambda () (interactive) (text-scale-increase 0)))   ; overrides er/expand-region
#+end_src

Undo and redo.

#+begin_src emacs-lisp
(global-set-key (kbd "C-z") 'undo)   ;overrides suspend-frame
(global-set-key (kbd "C-S-z") 'redo)
#+end_src

Various keybindings.

#+begin_src emacs-lisp
(global-set-key (kbd "<kp-7>") (lambda () "" (interactive) (find-file "~/.plan")))
(global-set-key (kbd "<kp-8>") (lambda () (interactive) (diary) (other-window 1)))
(global-set-key (kbd "<kp-9>") 'calendar)
(global-set-key (kbd "C-h n") 'man)  ;overrides view-emacs-news
#+end_src

** Display incomplete key commands

Display the key bindings following your currently entered incomplete
command (prefix) in a popup.

#+begin_src emacs-lisp
(use-package which-key
  :init
  (which-key-mode)
  (which-key-setup-side-window-right-bottom)
  (setq which-key-idle-delay 1.0))
#+end_src

** Change M-w to copy current line if region not selected

[[https://github.com/leoliu/easy-kill][easy-kill]] provides commands to let users kill or mark things easily.

~M-w~ alone saves in the order of active region, url, email and
finally the current line.

~M-w~ can also be used as a prefix key:
+ ~M-w w~ - save word at point
+ ~M-w s~ - save sexp at point
+ ~M-w l~ - save list at point (enclosing sexp)
+ ~M-w d~ - save defun at point
+ ~M-w D~ - save current defun name
+ ~M-w f~ - save file at point
+ ~M-w b~ - save buffer-file-name

For example, ~M-w w~ saves the current word, and repeating ~w~ expands
the kill to include the next word.

#+begin_src emacs-lisp
(use-package easy-kill
  :config
  (global-set-key [remap kill-ring-save] 'easy-kill))
#+end_src

** Configure ~Mouse-2~ to yank at point instead of at click

Do not move point on ~Mouse-2~; just insert the text at point,
regardless of where ~Mouse-2~ was clicked.  This is especially
important when using a touchpad.

#+begin_src emacs-lisp
(setq mouse-yank-at-point t)
#+end_src

** Configure clipboard interactions

Use the "clipboard" selection (the one typically is used by C-c/C-v)
instead of the X-Windows primary selection (which uses
mouse-select/middle-button-click).

See http://hugoheden.wordpress.com/2009/03/08/copypaste-with-emacs-in-terminal/

#+begin_src emacs-lisp
(setq x-select-enable-clipboard t)
#+end_src

If emacs is run in a terminal, the clipboard functions have no effect.
We use xsel instead.  If running under cygwin, we need to modify to
use putclip/getclip instead or xsel.

#+begin_src emacs-lisp
(unless window-system
  (when (getenv "DISPLAY")
    ;; Callback for when user cuts
    (defun xsel-cut-function (text &optional push)
      ;; Insert text to temp-buffer, and "send" content to xsel stdin
      (with-temp-buffer
        (insert text)
        ;; I prefer using the "clipboard" selection (the one the
        ;; typically is used by c-c/c-v) before the primary selection
        ;; (that uses mouse-select/middle-button-click)
        (call-process-region (point-min) (point-max) "xsel" nil 0 nil "--clipboard" "--input")))
    ;; Call back for when user pastes
    (defun xsel-paste-function ()
      ;; Find out what is current selection by xsel. If it is different
      ;; from the top of the kill-ring (car kill-ring), then return
      ;; it. Else, nil is returned, so whatever is in the top of the
      ;; kill-ring will be used.
      (let ((xsel-output (shell-command-to-string "xsel --clipboard --output")))
        (unless (string= (car kill-ring) xsel-output)
          xsel-output)))
    ;; Attach callbacks to hooks
    (setq interprogram-cut-function 'xsel-cut-function)
    (setq interprogram-paste-function 'xsel-paste-function)
    ;; Idea from
    ;; http://shreevatsa.wordpress.com/2006/10/22/emacs-copypaste-and-x/
    ;; http://www.mail-archive.com/help-gnu-emacs@gnu.org/msg03577.html
    ))
#+end_src

* Configuration Documentation

This configuration uses [[orgmode.org][org mode]] and [[https://github.com/jwiegley/use-package][use-package]].

Use "M-x list-packages" to see available and installed list of packages.
Use "M-x package-install" to install a new package.

** Example Configurations
  - [[http://pages.sachachua.com/.emacs.d/Sacha.html][Sacha Chua]]
  - [[https://github.com/danielmai/.emacs.d/blob/master/config.org][Daniel Mai]]
  - [[http://www.howardism.org/Technical/Emacs/literate-devops.html][Literate devops at howardism.org]]
